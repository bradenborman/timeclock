plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'candyfactory'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	//DB Driver
	implementation 'com.mysql:mysql-connector-j'

	//Excel document
	implementation 'org.apache.poi:poi:5.3.0'
	implementation 'org.apache.poi:poi-ooxml:5.3.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

task copyClient(dependsOn: ":tc-client:clientBuild") {

	doFirst {
		delete "src/main/resources/static"
		delete "src/main/resources/templates"
	}
	doLast {
		copy {
			from new File(project(":tc-client").getProjectDir(), "build")
			into "src/main/resources/static"
		}
		copy {
			from new File(project(":tc-client").getProjectDir(), "build/index.html")
			into "src/main/resources/templates"
		}
	}
}

jar {
	enabled = false
}

task finalizeBuild(type: Copy) {
	dependsOn 'build'

	println "Renaming jar.."
	from layout.buildDirectory.file("libs/tc-server-${version}.jar")
	into layout.buildDirectory.dir("libs")
	rename "tc-server-${version}.jar", "timeclock.jar"
	doLast {
		delete layout.buildDirectory.file("libs/tc-server-${version}.jar")
	}
}

processResources.dependsOn(copyClient)
clean.dependsOn(":tc-client:clientClean")
build.finalizedBy(finalizeBuild)

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean